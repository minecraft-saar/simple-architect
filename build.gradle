plugins {
    id 'java'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '5.2.0'
    id 'pl.allegro.tech.build.axion-release' version '1.10.1'
    id 'maven-publish'
}
group 'de.saar.minecraft'
description 'simple-architect'

configurations.all {
  resolutionStrategy {
    force 'xml-apis:xml-apis:1.4.01'
  }
}

scmVersion {
    ignoreUncommittedChanges = false
    tag {
        prefix = 'release'
    }
}

version = scmVersion.version


java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(16)
    }
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        name = "GitHubPackages"
        url = uri("https://maven.pkg.github.com/minecraft-saar/simple-architect")
        credentials {
            username = project.findProperty("gpr.user") ?: System.getenv("GPR_USER")
            password = project.findProperty("gpr.key") ?: System.getenv("GPR_API_KEY")
        }
    }
    maven { url 'https://jitpack.io' }
    // We need this repository because alto uses a MacOS window menu
    // library that seems to be not packaged but is a transitive dependency
    // of alto.
    //maven { url "http://akci.coli.uni-saarland.de/artifactory/external"
    //        allowInsecureProtocol = true }
    maven {url 'https://raw.github.com/coli-saar/alto/repository/'}
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:5.7.2'
    implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.13.1'
    implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.13.1'
    implementation group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: '2.13.1'
    implementation "org.yaml:snakeyaml:1.25"
    implementation group: 'com.github.minecraft-saar', name: 'minecraft-nlg', version: '4a1eb700a3ff183fcfdcc0155d6ad884e3145b59'
    implementation group: "de.saar.minecraft.infrastructure", name:"networking", version:"1.2.5"
    implementation group: "de.saar.minecraft.infrastructure", name:"architect", version:"1.2.5"
    implementation 'de.saar.minecraft:jshop:1.2.7'
    runtimeOnly ('com.github.minecraft-saar:shared-resources') {
	version {
	    strictly 'release-1.1.20'
	}
    }
    implementation group: 'com.github.minecraft-saar', name: 'weight-estimator', version: '51941b62e8fbdee7dccf772716ee97f4520f4675'
}

mainClassName = 'de.saar.minecraft.simplearchitect.Main'
